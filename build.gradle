wrapper.gradleVersion = '5.0'

// Major.Minor build version - this should be considered the release version number, and should be incremented manually when required
def buildVersion = '1'

def currentVersion = "${buildVersion}.${rootProject.buildRevision}"

def buildSettingsFolder = 'build_settings'

println "Build version: $currentVersion"
println "Gradle wrapper version: ${wrapper.gradleVersion}"

buildscript { scriptHandler ->
    apply from: "$buildSettingsFolder/buildscript_repositories.gradle", to: scriptHandler

    dependencies {
        classpath 'com.netflix.nebula:nebula-publishing-plugin:+'
    }
}

allprojects {
    group = 'roramu.util'
    version = currentVersion
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'nebula.maven-publish'

    // Get external repositories from a separate file (to allow for CI automation to provide different values)
    apply from: rootProject.file("$buildSettingsFolder/repositories.gradle").absolutePath

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    }

    // Get publishing behavior from a separate file (to allow for CI automation to provide different values)
    apply from: rootProject.file("$buildSettingsFolder/publishing.gradle").absolutePath
}
